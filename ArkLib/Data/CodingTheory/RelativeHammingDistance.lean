import Mathlib.InformationTheory.Hamming
import Mathlib.Analysis.Normed.Field.Lemmas

open Classical

variable {ι : Type*} [Fintype ι]
         {F : Type*}
         {u v w c : ι → F}
         {C : Set (ι → F)}

@[simp]
lemma zero_lt_card [Nonempty ι] : 0 < Fintype.card ι := by have := Fintype.card_ne_zero (α := ι); omega

abbrev LinearCode.{u, v} (ι : Type u) (F : Type v) [Semiring F] : Type (max u v)
  := Submodule F (ι → F)

noncomputable section

/--
  weight of a vector
-/
def vector.wt [Zero F] (v : ι → F) : Finset ι := {i | v i ≠ 0}

namespace RelativeHamming

section

variable [Nonempty ι]

/--
  relative Hamming distance between vectors `u` and `v`
-/
def dist (u v : ι → F) : ℚ :=
  (hammingDist u v : ℚ) / (Fintype.card ι : ℚ)

@[simp]
lemma relHammingDist_le_one : dist u v ≤ 1 := by
  unfold dist
  rw [div_le_iff₀ (by simp)]
  simp [hammingDist_le_card_fintype]

@[simp]
lemma zero_le_relHammingDist : 0 ≤ dist u v := by
  unfold dist
  rw [le_div_iff₀ (by simp)]
  simp

end

/--
`δᵣ(u,v)` denotes the relative Hamming distance between vectors `u` and `v`
-/
notation "δᵣ(" u ", " v ")" => dist u v

def distRange (ι : Type*) [Fintype ι] : Set ℚ :=
  { d : ℚ | ∃ d' : ℕ, d' ≤ Fintype.card ι ∧ d = d' / Fintype.card ι }

@[simp]
lemma relHammingDist_mem_relHammingDistRange : δᵣ(u, v) ∈ distRange ι :=
  ⟨hammingDist _ _, Finset.card_filter_le _ _, rfl⟩

@[simp]
lemma finite_relHammingDistRange [Nonempty ι] : (distRange ι).Finite := by
  simp only [distRange, ← Set.finite_coe_iff, Set.coe_setOf]
  exact
    finite_iff_exists_equiv_fin.2
      ⟨Fintype.card ι + 1,
        ⟨⟨
        λ ⟨s, _⟩ ↦ ⟨(s * Fintype.card ι).num.toNat, by aesop (add safe (by omega))⟩,
        λ n ↦ ⟨n / Fintype.card ι, by use n; simp; omega⟩,
        λ ⟨_, _, _, h₂⟩ ↦ by field_simp [h₂],
        λ _ ↦ by simp
        ⟩⟩
      ⟩

@[simp]
lemma finite_offDiag [Finite F] : C.offDiag.Finite := Set.Finite.offDiag (Set.toFinite _)

/--
The set of possible distances between distinct codewords of `C`
-/
def d_C (C : Set (ι → F)) : Set ℚ :=
  { d : ℚ | ∃ p ∈ Set.offDiag C, δᵣ(p.1, p.2) = d }

section

@[simp]
lemma d_C_subset_relHammingDistRange : d_C C ⊆ distRange ι :=
  λ _ _ ↦ by aesop (add simp d_C)

@[simp]
lemma finite_d_C [Nonempty ι] : (d_C C).Finite :=
  Set.Finite.subset finite_relHammingDistRange d_C_subset_relHammingDistRange

end

/--
relative Hamming Distance of a code C over a semiring F
-/
def minDistCode [Nonempty ι] (C : Set (ι → F)) : ℚ :=
  have : Fintype (d_C C) := @Fintype.ofFinite _ finite_d_C
  if h : (d_C C).Nonempty
  then (d_C C).toFinset.min' (Set.toFinset_nonempty.2 h)
  else 0

/--
`δᵣ C` denotes the minimum relative Hamming distance of a code `C`
-/
notation "δᵣ" C => minDistCode C

/--
`d_w` is the set of values of relHamming distance between a vector `w` and codewords `c ∈ C`.
-/
def d_w (w : ι → F) (C : Set (ι → F)) : Set ℚ :=
  { d : ℚ | ∃ c ∈ C, c ≠ w ∧ δᵣ(w, c) = d }

@[simp]
lemma d_w_subset_relHammingDistRange : d_w w C ⊆ distRange ι :=
  λ d h ↦ by aesop (add simp d_w)

@[simp]
lemma finite_d_w [Nonempty ι] : (d_w w C).Finite :=
  Set.Finite.subset finite_relHammingDistRange d_w_subset_relHammingDistRange

instance [Nonempty ι] : Fintype (d_w w C) := @Fintype.ofFinite _ finite_d_w

--Q: Do we still want this counterexample?
/--
  Counter example to d_wNonempty,
  need an extra condition that C has at least two elements or
  w isn't in C and C is non-empty?
-/
example : d_w w {w} = ∅ := by
  unfold d_w
  simp

/--
relative Hamming distance between a vector `w` and a code `C`
-/

def distToCode [Nonempty ι] (w : ι → F) (C : Set (ι → F)) : ℚ :=
  if h : (d_w w C).Nonempty
  then (d_w w C).toFinset.min' (Set.toFinset_nonempty.2 h)
  else 0

/--
`δᵣ(w,C)` denotes the relative Hamming distance between a vector `w` and a code `C`
-/
notation "δᵣ(" w ", " C ")" => distToCode w C

@[simp]
lemma zero_mem_RelHammingDistRange : 0 ∈ distRange ι := by use 0; simp

@[simp]
lemma relHammingDistToCode_mem_relHammingDistRange [Nonempty ι] :
  δᵣ(c, C) ∈ distRange ι := by
  unfold distToCode
  split_ifs
  · exact Set.mem_of_subset_of_mem (s₁ := (d_w c C).toFinset) (by simp) (Finset.min'_mem _ _)
  · simp

end RelativeHamming
end
